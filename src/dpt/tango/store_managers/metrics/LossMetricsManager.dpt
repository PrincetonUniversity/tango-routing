include "../../Types.dpt"

/**
 * # Description
 * Keep track of loss metrics
 * 
 * # Methods
 * create() -> LossMetricsManager.t
 * update_loss(LossMetricsManager.t, int, int<<24>>, int<<3>>) -> int<<32>>
 **/
module LossMetricsManager : {
    global type t;
    constr t create ();

   /**
    * # Description
    * Updates the loss from the incoming Tango packets sequence number.
    * 
    * # Parameters
    * self (t): self
    * sequence_num (int<<24>>): sequence number of packet
    * path_id (int<<3>>): the path on which the packet came
    * 
    * # Returns
    * metrics (int<<32>>): the calculated metrics
    **/
    fun int<<32>> update_loss (
        t self,
        int avg_counter,
        int<<24>> sequence_num,
        int<<3>> path_id
    ) [start <= self; end self];
}
{
    type t = {
        Array.t<<24>> seq_num_tracker;
        Array.t<<32>> loss_sums;
        Array.t<<32>> loss_avgs;
    }

    constr t create () = {
        seq_num_tracker = Array.create(8);
        loss_sums = Array.create(8);
        loss_avgs = Array.create(8);
    };

    /**
     * # Description
     * Store the larger sequence number. 
     **/
    memop store_larger (int<<'sz>> curr_seq_num, int<<'sz>> new_seq_num) {
        if (curr_seq_num < new_seq_num) {
            return new_seq_num;
        } else {
            return curr_seq_num;
        }
    }

    /**
     * # Description
     * Get the loss between this and the previously seen sequence number. 
     **/
    memop calc_loss (int<<'sz>> curr_seq_num, int<<'sz>> new_seq_num) {
        if (curr_seq_num + 1 < new_seq_num) {
            return new_seq_num - curr_seq_num;
        } else {
            return 0;
        }
    }

    /**
     * # Description
     * Update the stored average loss.
     **/
    memop update_loss_avg (
        int<<'sz>> curr_loss_avg,
        int<<'sz>> loss_sum,
        int<<'sz>> curr_count
    ) {
        bool should_reset = curr_count > 62;

        if (should_reset) {
            cell1 = loss_sum;
        }

        if (true) { return cell1; }
    }

    /**
     * # Description
     * Update the stored running sum of loss.
     **/
    memop update_loss_sum (
        int<<'sz>> curr_loss_sum,
        int<<'sz>> new_loss,
        int<<'sz>> curr_count
    ) {
        bool should_reset = curr_count > 62;

        if (should_reset) {
            cell1 = 0;
        } else {
            if (true) { cell1 = curr_loss_sum + new_loss; }
        }

        if (should_reset) {
            return curr_loss_sum + new_loss;
        }
    }

    fun int<<32>> update_loss (
        t self,
        int avg_counter,
        int<<24>> sequence_num,
        int<<3>> path_id
    ) {
        // Calculate loss and updated last seen sequence number
        int<<32>> loss_found = (int<<32>>) Array.update(
            self#seq_num_tracker,
            path_id,
            calc_loss,
            sequence_num,
            store_larger,
            sequence_num
        );

        // Update running sum of losses
        int<<32>> curr_loss_sum = Array.update_complex(
            self#loss_sums,
            path_id,
            update_loss_sum,
            loss_found,
            avg_counter,
            0
        );

        // Update loss average
        int<<32>> avg_loss = Array.update_complex(
            self#loss_avgs,
            path_id,
            update_loss_avg,
            curr_loss_sum,
            avg_counter,
            0
        );

        printf("loss: %d, loss-sum: %d, loss-avg: %d", loss_found, curr_loss_sum, avg_loss);

        return avg_loss;
    }
}
