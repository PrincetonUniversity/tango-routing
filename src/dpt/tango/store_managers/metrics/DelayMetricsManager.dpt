include "../../Types.dpt"
include "../../Time.dpt"

/**
 * # Description
 * Keep track of delay metrics
 * 
 * # Methods
 * create() -> DelayMetricsManager.t
 * update_delay(DelayMetricsManager.t, int, int<<12>>, int<<3>>) -> int<<32>>
 **/
module DelayMetricsManager : {
    global type t;
    constr t create ();

   /**
    * # Description
    * Updates the delay from the incoming Tango packets timestamp
    * 
    * # Parameters
    * self (t): self
    * timestamp (int<<12>>): sequence number to index into pads
    * path_id (int<<3>>): the path on which the packet came
    * 
    * # Returns
    * metrics (int<<32>>): the calculated metrics
    **/
    fun int<<32>> update_delay (
        t self,
        int avg_counter,
        int<<12>> timestamp,
        int<<3>> path_id
    ) [start <= self; end self];

}
{
    type t = {
        Array.t<<32>> delay_sums;
        Array.t<<32>> delay_avgs;
    }

    constr t create() = {
        delay_sums = Array.create(8);
        delay_avgs = Array.create(8);
    };

    /**
     * # Description
     * Update the stored average delay.
     **/
    memop update_delay_avg (
        int<<'sz>> curr_delay_avg,
        int<<'sz>> delay_sum,
        int<<'sz>> curr_count
    ) {
        bool should_reset = curr_count > 62;

        if (should_reset) {
            cell1 = delay_sum;
        }

        if (true) { return cell1; }
    }

    /**
     * # Description
     * Update the stored running sum of delay.
     **/
    memop update_delay_sum (
        int<<'sz>> curr_delay_sum,
        int<<'sz>> new_delay,
        int<<'sz>> curr_count
    ) {
        bool should_reset = curr_count > 62;

        if (should_reset) {
            cell1 = 0;
        } else {
            if (true) { cell1 = curr_delay_sum + new_delay; }
        }

        if (should_reset) {
            return curr_delay_sum + new_delay;
        }
    }

    fun int<<32>> update_delay (
        t self,
        int avg_counter,
        int<<12>> timestamp,
        int<<3>> path_id
    ) {
        // Calculate delay
        int<<12>> curr_time = get_time_now_ms();
        int new_delay = (int<<32>>) (curr_time - timestamp);
        printf("curr_time: %d, timestamp: %d", curr_time, timestamp);

        // Update running sum of delays
        int<<32>> delay_sum = Array.update_complex(
            self#delay_sums,
            path_id,
            update_delay_sum,
            new_delay,
            avg_counter,
            0
        );

        // Update average delay
        int<<32>> delay_avg = Array.update_complex(
            self#delay_avgs,
            path_id,
            update_delay_avg,
            delay_sum,
            avg_counter,
            0
        );

        return delay_avg;
    }
}
