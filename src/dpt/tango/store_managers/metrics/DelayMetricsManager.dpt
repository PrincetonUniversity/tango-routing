include "../../Types.dpt"
include "../../Time.dpt"

/**
 * # Description
 * Keep track of delay metrics
 * 
 * # Methods
 * create() -> DelayMetricsManager.t
 * update_delay(DelayMetricsManager.t, int, int<<3>>) -> int<<32>>
 * get_metrics(DelayMetricsManager.t, int, int<<3>>) -> int<<32>>
 **/
module DelayMetricsManager : {
    global type t;
    constr t create ();

   /**
    * # Description
    * Updates the delay from the incoming Tango packets timestamp
    * 
    * # Parameters
    * self (t): self
    * timestamp (int<<12>>): sequence number to index into pads
    * path_id (int<<3>>): the path on which the packet came
    * 
    * # Returns
    * metrics (int<<32>>): the calculated metrics
    **/
    fun int<<32>> update_delay (
        t self,
        int<<12>> timestamp,
        int<<3>> path_id
    ) [start <= self; end self];

}
{
    type t = {
        Counter.t<<5>> delay_count0;
        Counter.t<<5>> delay_count1;
        Counter.t<<5>> delay_count2;
        Counter.t<<5>> delay_count3;
        Counter.t<<5>> delay_count4;
        Counter.t<<5>> delay_count5;
        Counter.t<<5>> delay_count6;
        Counter.t<<5>> delay_count7;
        PairArray.t<<32>> delay_sums;
    }

    constr t create() = {
        delay_count0 = Counter.create(0);
        delay_count1 = Counter.create(0);
        delay_count2 = Counter.create(0);
        delay_count3 = Counter.create(0);
        delay_count4 = Counter.create(0);
        delay_count5 = Counter.create(0);
        delay_count6 = Counter.create(0);
        delay_count7 = Counter.create(0);
        delay_sums = PairArray.create(8);
    };

    /**
     * # Description
     * Update the running sum of delays with the newest delay
     * measurement and return it. If hit average count threshold, reset.
     **/
    memop update_delay_avg (
        int<<'sz>> curr_delay_sum,
        int<<'sz>> curr_delay_avg,
        int<<'sz>> new_delay,
        int<<'sz>> curr_count
    ) {
        bool should_reset = curr_count > 30;

        if (should_reset) {
            cell1 = 0;
        } else {
            if (true) { cell1 = curr_delay_sum + new_delay; }
        }

        if (should_reset) {
            cell2 = curr_delay_sum;
        }
        
        if (true) { return cell2; }
    }


    fun int<<32>> update_delay (
        t self,
        int<<12>> timestamp,
        int<<3>> path_id
    ) {
        // Count this packet as seen
        int delay_count = 0;
        match path_id with
        | 0 -> { delay_count = (int<<32>>) Counter.add(self#delay_count0, 1) + 1; }
        | 1 -> { delay_count = (int<<32>>) Counter.add(self#delay_count1, 1) + 1; }
        | 2 -> { delay_count = (int<<32>>) Counter.add(self#delay_count2, 1) + 1; }
        | 3 -> { delay_count = (int<<32>>) Counter.add(self#delay_count3, 1) + 1; }
        | 4 -> { delay_count = (int<<32>>) Counter.add(self#delay_count4, 1) + 1; }
        | 5 -> { delay_count = (int<<32>>) Counter.add(self#delay_count5, 1) + 1; }
        | 6 -> { delay_count = (int<<32>>) Counter.add(self#delay_count6, 1) + 1; }
        | 7 -> { delay_count = (int<<32>>) Counter.add(self#delay_count7, 1) + 1; }

        // Update and get delay
        int<<12>> curr_time = get_time_now_ms();
        int ts_diff = (int<<32>>) (curr_time - timestamp);
        printf("Found ts diff: %d - %d = %d", curr_time, timestamp, ts_diff);
        int<<32>> delay_avg = PairArray.update(
            self#delay_sums,
            path_id,
            update_delay_avg,
            ts_diff,
            delay_count,
            0
        );

        // Divide by '32'
        return (int<<32>>) delay_avg[31:5];
    }
}
