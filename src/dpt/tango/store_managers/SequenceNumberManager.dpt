/**
 * # Description
 * Keep track of path-secific sequence numbers.
 * 
 * # Methods
 * create() -> SequenceNumberManager.t
 * increment(SequenceNumberManager.t, int<<3>>) -> int<<24>>
 **/
module SequenceNumberManager : {
    global type t;
    constr t create ();

   /**
    * # Description
    * Increments and returns the current, non-incremented sequence
    * number of a given path.
    * 
    * # Parameters
    * self (t): self
    * path_id (int<<3>>): the path on which the packet is tunneled
    * 
    * # Returns
    * sequence_number (int<<24>>): the current, non-incremented sequence number
    **/
    fun int<<24>> increment (
        t self,
        int<<3>> path_id
    ) [start <= self; end self];
}
{
    type t = {
        Array.t<<24>> seq_nums;
    }

    constr t create () = {
        seq_nums = Array.create(8);
    };

    /**
     * # Description
     * Set next sequence number for this path.
     **/
    memop next(int<<'sz>> curr, int<<'sz>> inc) {
        return curr + inc;
    }

    /**
     * # Description
     * Get current sequence number for this path.
     **/
    memop get_curr(int<<'sz>> curr, int<<'sz>> inc) {
        return curr;
    }

    fun int<<24>> increment (
        t self,
        int<<3>> path_id
    ) {
        return Array.update(
            self#seq_nums,
            path_id,
            get_curr,
            0,
            next,
            (int<<24>>) 1
        );
    }
}
