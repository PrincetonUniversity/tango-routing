include "../../../Types.dpt"

/**
 * # Description
 * Encrypt and decrypt route updates using a one-time pad
 * 
 * # Methods
 * create() -> Decryptor.t
 * decrypt(Decryptor.t, int<<64>>) -> int<<64>>
 **/
module Decryptor : {
    global type t;
    constr t create ();

   /**
    * # Description
    * Decrypt the route update.
    * 
    * # Parameters
    * self (t): self
    * seq_number (int<<4>>): sequence number of update
    * ciphertext (int<<64>>): signature to validate
    * 
    * # Returns
    * plaintext (int<<64>>): decrypted route update
    **/
    fun int<<64>> decrypt (
        t self,
        int<<4>> seq_number,
        int<<64>> ciphertext
    ) [start <= self; end self];

   /**
    * # Description
    * Set the one-time pad associated with a sequence number.
    * 
    * # Parameters
    * self (t): self
    * sequence_num (int<<24>>): route update sequence number
    * otp (int<<64>>): one-time pad to decrypt with
    **/
    fun void control_set (
        t self,
        int<<4>> sequence_num,
        int<<64>> otp
    ) [start <= self; end self];
}
{
    type t = {
        Array.t<<64>> corresponding_otps;
    }

    constr t create () = {
        corresponding_otps = Array.create(16);
    };

    fun int<<64>> decrypt (
        t self,
        int<<4>> seq_number,
        int<<64>> ciphertext
    ) {
        int<<64>> pad = Array.get(self#corresponding_otps, seq_number);

        return ciphertext ^^ pad;
    }
    
    fun void control_set (
        t self,
        int<<4>> sequence_num,
        int<<64>> otp
    ) {
        Array.set(self#corresponding_otps, sequence_num, otp);
    }
}
