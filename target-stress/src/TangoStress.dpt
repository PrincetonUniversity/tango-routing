include "Time.dpt"
include "Types.dpt"
include "static_maps/TrafficClassMap.dpt"
include "static_maps/TunnelHeaderMap.dpt"
include "store_managers/SequenceNumberManager.dpt"
include "store_managers/cryptography/signatures/BookSignatureManager.dpt"
include "store_managers/cryptography/signatures/MetricSignatureManager.dpt"

/* ================================================================================================================== *
 *                                                     EXTERNS                                                        *
 * ================================================================================================================== */

// Port declarations
const int<<9>> OUT_PORT = 128;

// Tango UDP header to counter ECMP
const UDPHeader_t TANGO_UDP_HEADER = {
    source_port = 8080;
    destination_port = 8080;
    length = 0;
    checksum = 0;
};

/* ================================================================================================================== *
 *                                                     GLOBALS                                                        *
 * ================================================================================================================== */

// Counter keeping track of tango sequence numbers w.r.t. outgoing flow packets
global SequenceNumberManager.t sequence_counters = SequenceNumberManager.create();

/* ================================================================================================================== *
 *                                                     EVENTS                                                         *
 * ================================================================================================================== */

/**
 * # Description
 * Application packet tunneled through system.
 * 
 * # Parameters
 * eth_header (`EthernetHeader_t`): ethernet frame header
 * ip_header (`IPv4Header_t`): IPv4 header of incoming packet
 * udp_header (`UDPHeader_t`): TCP header of incoming packet
 *
 * # Generates
 * forward_tango_traffic: flow packet wrapped with tango data sent to peer node
 * complete_forward: not tango traffic
 **/
entry event forward_flow (
    EthernetHeader_t eth_header,
    IPv4Header_t ip_header,
    UDPHeader_t udp_header
) [ sequence_counters <= sequence_counters ];

/**
 * # Description
 * Egress event for forwarding a flow
 **/
event attach_signatures (
    EthernetHeader_t tango_eth_header,
    IPv6Header_t tango_ip_header,
    UDPHeader_t tango_udp_header,
    int<<16>> seq_number,
    int<<8>> traffic_class,
    int<<16>> timestamp,
    IPv4Header_t encaped_ip_header,
    UDPHeader_t encaped_udp_header
);

/**
 * # Description
 * Incoming Tango data traffic from peer node.
 * 
 * # Parameters
 * tango_eth_header (EthernetHeader_t): tunneled ethernet frame header
 * tango_ip_header (IPv6Header_t): tango tunnel IPv6 header
 * tango_tango_udp_header (UDPHeader_t): udp header to counter ECMP
 * tango_metrics_header (TangoHeader_t): tango metric data and signatures
 * encaped_ip_header (IPv4Header_t): flow IPv4 header tunneled thrpugh path
 * encaped_udp_header (UDPHeader_t): tcp header of tunneled packet
 *
 * # Generates
 * request_route_update: sometimes responds to peer with route mapping update
 * complete_forward: forwards the packet on to its final destination
 **/
exit event incoming_tango_traffic (
    EthernetHeader_t tango_eth_header,
    IPv6Header_t tango_ip_header,
    UDPHeader_t tango_udp_header,
    TangoHeader_t tango_metrics_header,
    IPv4Header_t encaped_ip_header,
    UDPHeader_t encaped_udp_header
);

/* ================================================================================================================== *
 *                                                    HANDLERS                                                        *
 * ================================================================================================================== */

handle forward_flow (EthernetHeader_t eth_header, IPv4Header_t ip_header, UDPHeader_t udp_header) {

    /* 1. Map flow to a specific physical route. */
    // -- Map flow to traffic class.
    int<<8>> traffic_class = map_flow_to_traffic_class(
        ip_header#src_address,
        udp_header#source_port,
        ip_header#dest_address,
        udp_header#destination_port,
        ip_header#protocol
    );

    // -- Get physical path header to tunnel with.
    IPv6Header_t tango_tunnel_hdr = map_path_to_tunnel_header((int<<8>>) traffic_class);

    /* 2. Get metric values. */
    // -- Get updated sequence number
    int<<16>> seq_number = SequenceNumberManager.increment(sequence_counters, (int<<8>>) traffic_class);

    // -- Get current time in milliseconds
    int<<12>> timestamp = get_time_now_ms();
    
    event egress_packet = attach_signatures (
        eth_header,
        { tango_tunnel_hdr with payload_len = 88 + ip_header#len },
        TANGO_UDP_HEADER,
        seq_number,
        traffic_class,
        (int<<16>>) timestamp,
        ip_header,
        udp_header
    );

    generate_port (ingress_port, egress_packet);
}

// Provide read access to precomputed 1-bit signatures derived from sequence numbers
global BookSignatureManager.t outgoing_book_signature_manager = BookSignatureManager.create();

// Provide read access to precomputed timestamp + path ID signatures
global MetricSignatureManager.t outgoing_metric_signature_manager = MetricSignatureManager.create();

@egress
handle attach_signatures (
    EthernetHeader_t tango_eth_header,
    IPv6Header_t tango_ip_header,
    UDPHeader_t tango_udp_header,
    int<<16>> seq_number,
    int<<8>> traffic_class,
    int<<16>> timestamp,
    IPv4Header_t encaped_ip_header,
    UDPHeader_t encaped_udp_header
) {
    /* 3. Sign metric values. */
    // -- Get signature corresponding to sequence number
    int<<1>> book_signature = BookSignatureManager.sign(outgoing_book_signature_manager, seq_number, (int<<3>>) traffic_class);

    // -- Get precomputed path-specific signature
    int<<32>> ts_signature = MetricSignatureManager.sign(outgoing_metric_signature_manager, (int<<12>>) timestamp, (int<<3>>) traffic_class);


    /* 4. Encapsulate and forward packet. */
    TangoHeader_t tango_metrics_hdr = {
        path_id = (int<<8>>) traffic_class;
        timestamp = timestamp;
        signature = ts_signature;
        sequence_num = seq_number;
        book_signature = (int<<8>>) book_signature;
    };

    event forward_tango_pkt = incoming_tango_traffic (
        tango_eth_header,
        tango_ip_header,
        tango_udp_header,
        tango_metrics_hdr,
        encaped_ip_header,
        encaped_udp_header
    );

    printf("Encapsulating packet with: { path_id: %d, timestamp: %d, ts_signature: %d, seq_number: %d, book_signature: %d, port: %d }",
        (int<<3>>) traffic_class,
        timestamp,
        ts_signature,
        seq_number,
        book_signature,
        OUT_PORT
    );

    generate_port (OUT_PORT, forward_tango_pkt);
}